// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {
    field Array string;
    field int str_len, max_len;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {
        if (maxLength = 0) { let maxLength = 1; }
        let string = Array.new(maxLength);
        let max_len = maxLength;
        let str_len = 0;
        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do Array.dispose(string);
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return str_len;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        return string[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        let string[j] = c;
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        if (str_len < max_len) {
            let string[str_len] = c;
            let str_len = str_len + 1;
        }
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        if (str_len > 0) {
            let str_len = str_len - 1;
        }
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        var int int_val;
        var int i;
        var boolean neg;

        let int_val = 0;

        if ((str_len > 0) & (string[0] = 45)) {
            let neg = true;
            let i = 1;
        }
        else {
            let neg = false;
            let i = 0;
        }
        while ((i < str_len) & String.is_digit(string[i])) {
            let int_val = (int_val * 10) + (string[i] - 48);
            let i = i + 1;
        }
        if (neg) { return -int_val; }
        else { return int_val; }
    }

    function boolean is_digit(char c) {
        return (47 < c) & (c < 58);     // 48 <= c <= 57
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        let str_len = 0;

        if (val < 0) {
            let val = -val;
            do appendChar(45);      // minus sign '-'
        }

        do setCharofInt(val);
        return;
    }

    method void setCharofInt(int val) {
        var int quotient;
        var int remainder;
        var char c;

        let quotient = val / 10;
        let remainder = val - (quotient * 10);
        let c = remainder + 48;     // e.g. '1' = 1 + 48

        if (val > 9) { do setCharofInt(quotient); }
        do appendChar(c);
        return;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}
