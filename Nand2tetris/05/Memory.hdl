/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    	Not(in=address[14], out=selmemo);            // 0## #### #### ####
    	Not(in=address[13], out=not13);
    	And(a=address[14], b=not13, out=selscreen);  // 10# #### #### ####
    	And(a=address[14], b=address[13], out=selkb1);
    	Or8Way(in=address[5..12], out=orout1);
    	Or(a=address[0], b=address[1], out=or01);
    	Or(a=address[2], b=address[3], out=or23);
    	Or(a=or01, b=or23, out=or0123);
    	Or(a=or0123, b=address[4], out=orout2);
    	And(a=orout1, b=orout2, out=notorout);
    	Not(in=notorout, out=selkb2);
    	And(a=selkb1, b=selkb2, out=selkb);          // 110 0000 0000 0000

    	And(a=selmemo, b=load, out=loadmemo);
    	And(a=selscreen, b=load, out=loadscreen);

    	RAM16K(in=in, load=loadmemo, address=address[0..13], out=a);
    	Screen(in=in, load=loadscreen, address=address[0..12], out=b);
    	Keyboard(out=c);

    	Mux16(a=false, b=c, sel=selkb, out=muxout1);
    	Mux16(a=muxout1, b=b, sel=selscreen, out=muxout2);
    	Mux16(a=muxout2, b=a, sel=selmemo, out=out);
}